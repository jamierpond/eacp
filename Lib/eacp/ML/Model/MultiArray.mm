#include "MultiArrayNative.h"

#include <eacp/Core/ObjC/AutoReleasePool.h>
#include <eacp/Core/ObjC/CFRef.h>
#include <eacp/Core/ObjC/ObjC.h>
#include <eacp/GPU/GPU.h>

#import <CoreVideo/CoreVideo.h>

#include <Accelerate/Accelerate.h>

#include <algorithm>
#include <cstring>
#include <limits>

namespace eacp::ML
{
// A surface-backed array keeps no pointer: its base address is only valid
// while the pixel buffer is locked, so every CPU access locks it for its own
// duration. bytes is the plain array's memory, owned by the MLMultiArray.
struct MultiArray::Native
{
    Shape shape;
    DType type = DType::float32;
    ObjC::Ptr<MLMultiArray> array;
    CFRef<CVPixelBufferRef> surface;
    std::byte* bytes = nullptr;
    size_t stride = 0;
};

namespace
{
int multiArrayElementSizeOf(DType type)
{
    return type == DType::float16 ? 2 : 4;
}

NSArray<NSNumber*>* multiArrayShapeToNSArray(const Shape& shape)
{
    auto dimensions = [NSMutableArray arrayWithCapacity:(NSUInteger) shape.dims.size()];

    for (auto dimension: shape.dims)
        [dimensions addObject:@(dimension)];

    return dimensions;
}
} // namespace

Shape toShape(NSArray<NSNumber*>* dimensions)
{
    auto shape = Shape {};

    for (NSNumber* dimension in dimensions)
        shape.dims.add(dimension.intValue);

    return shape;
}

bool toDType(MLMultiArrayDataType dataType, DType& type)
{
    if (dataType == MLMultiArrayDataTypeFloat32)
    {
        type = DType::float32;
        return true;
    }

    if (dataType == MLMultiArrayDataTypeInt32)
    {
        type = DType::int32;
        return true;
    }

    if (@available(macOS 12.0, iOS 16.0, *))
    {
        if (dataType == MLMultiArrayDataTypeFloat16)
        {
            type = DType::float16;
            return true;
        }
    }

    return false;
}

namespace
{
float readElement(const std::byte* row, DType type, int column)
{
    if (type == DType::int32)
        return (float) reinterpret_cast<const std::int32_t*>(row)[column];

    return reinterpret_cast<const float*>(row)[column];
}

void writeElement(std::byte* row, DType type, int column, float value)
{
    if (type == DType::int32)
        reinterpret_cast<std::int32_t*>(row)[column] = (std::int32_t) value;
    else
        reinterpret_cast<float*>(row)[column] = value;
}

vImage_Buffer planeOf(const void* data, int rows, int columns, size_t stride)
{
    return {const_cast<void*>(data),
            (vImagePixelCount) rows,
            (vImagePixelCount) columns,
            stride};
}

void copyRows(const std::byte* source,
              size_t sourceStride,
              std::byte* destination,
              size_t destinationStride,
              int rows,
              size_t rowBytes)
{
    if (sourceStride == rowBytes && destinationStride == rowBytes)
    {
        std::memcpy(destination, source, rowBytes * (size_t) rows);
        return;
    }

    for (auto row = 0; row < rows; ++row)
        std::memcpy(destination + (size_t) row * destinationStride,
                    source + (size_t) row * sourceStride,
                    rowBytes);
}

void convertThroughFloat(const std::byte* source,
                         DType sourceType,
                         size_t sourceStride,
                         std::byte* destination,
                         DType destinationType,
                         size_t destinationStride,
                         int rows,
                         int columns)
{
    for (auto row = 0; row < rows; ++row)
    {
        auto from = source + (size_t) row * sourceStride;
        auto to = destination + (size_t) row * destinationStride;

        for (auto column = 0; column < columns; ++column)
            writeElement(
                to, destinationType, column, readElement(from, sourceType, column));
    }
}

void convertWidening(const void* source,
                     size_t sourceStride,
                     void* destination,
                     size_t destinationStride,
                     int rows,
                     int columns)
{
    auto from = planeOf(source, rows, columns, sourceStride);
    auto to = planeOf(destination, rows, columns, destinationStride);
    vImageConvert_Planar16FtoPlanarF(&from, &to, kvImageNoFlags);
}

void convertNarrowing(const void* source,
                      size_t sourceStride,
                      void* destination,
                      size_t destinationStride,
                      int rows,
                      int columns)
{
    auto from = planeOf(source, rows, columns, sourceStride);
    auto to = planeOf(destination, rows, columns, destinationStride);
    vImageConvert_PlanarFtoPlanar16F(&from, &to, kvImageNoFlags);
}

// Rows of columns elements from one layout and type to another. Strides are in
// bytes; fp16 <-> fp32 goes through vImage, which honours both strides.
void multiArrayConvertRows(const void* source,
                           DType sourceType,
                           size_t sourceStride,
                           void* destination,
                           DType destinationType,
                           size_t destinationStride,
                           int rows,
                           int columns)
{
    if (source == nullptr || destination == nullptr || rows <= 0 || columns <= 0)
        return;

    auto from = static_cast<const std::byte*>(source);
    auto to = static_cast<std::byte*>(destination);

    if (sourceType == destinationType)
    {
        auto rowBytes =
            (size_t) columns * (size_t) multiArrayElementSizeOf(sourceType);
        copyRows(from, sourceStride, to, destinationStride, rows, rowBytes);
        return;
    }

    if (sourceType == DType::float16 && destinationType == DType::float32)
    {
        convertWidening(from, sourceStride, to, destinationStride, rows, columns);
        return;
    }

    if (sourceType == DType::float32 && destinationType == DType::float16)
    {
        convertNarrowing(from, sourceStride, to, destinationStride, rows, columns);
        return;
    }

    if (sourceType == DType::float16)
    {
        auto widened = Vector<float> {};
        widened.resize(rows * columns, 0.0f);
        auto widenedStride = (size_t) columns * sizeof(float);
        convertWidening(from, sourceStride, widened.data(), widenedStride, rows, columns);
        convertThroughFloat(reinterpret_cast<const std::byte*>(widened.data()),
                            DType::float32,
                            widenedStride,
                            to,
                            destinationType,
                            destinationStride,
                            rows,
                            columns);
        return;
    }

    if (destinationType == DType::float16)
    {
        auto staged = Vector<float> {};
        staged.resize(rows * columns, 0.0f);
        auto stagedStride = (size_t) columns * sizeof(float);
        convertThroughFloat(from,
                            sourceType,
                            sourceStride,
                            reinterpret_cast<std::byte*>(staged.data()),
                            DType::float32,
                            stagedStride,
                            rows,
                            columns);
        convertNarrowing(staged.data(), stagedStride, to, destinationStride, rows, columns);
        return;
    }

    convertThroughFloat(from,
                        sourceType,
                        sourceStride,
                        to,
                        destinationType,
                        destinationStride,
                        rows,
                        columns);
}

bool isAllocatable(const Shape& shape)
{
    auto isEmptyOrUnknown = [](int size) { return size <= 0; };

    return !shape.dims.empty() && shape.isFixed()
           && shape.dims.findIf(isEmptyOrUnknown) == nullptr
           && shape.count() <= std::numeric_limits<int>::max();
}

class CpuAccess
{
public:
    CpuAccess(const MultiArray::Native& nativeToUse, bool readOnly)
        : native(nativeToUse)
        , flags(readOnly ? kCVPixelBufferLock_ReadOnly : 0)
    {
        if (native.surface)
        {
            CVPixelBufferLockBaseAddress(native.surface, flags);
            bytes = static_cast<std::byte*>(CVPixelBufferGetBaseAddress(native.surface));
        }
        else
        {
            bytes = native.bytes;
        }
    }

    ~CpuAccess()
    {
        if (native.surface)
            CVPixelBufferUnlockBaseAddress(native.surface, flags);
    }

    CpuAccess(const CpuAccess&) = delete;
    CpuAccess& operator=(const CpuAccess&) = delete;

    std::byte* data() const { return bytes; }

private:
    const MultiArray::Native& native;
    CVPixelBufferLockFlags flags;
    std::byte* bytes = nullptr;
};

bool toMLDataType(DType type, MLMultiArrayDataType& dataType)
{
    if (type == DType::int32)
    {
        dataType = MLMultiArrayDataTypeInt32;
        return true;
    }

    if (type == DType::float32)
    {
        dataType = MLMultiArrayDataTypeFloat32;
        return true;
    }

    if (@available(macOS 12.0, iOS 16.0, *))
    {
        dataType = MLMultiArrayDataTypeFloat16;
        return true;
    }

    return false;
}

bool readRowStride(MLMultiArray* array, int elementSize, size_t& stride)
{
    auto shape = array.shape;
    auto strides = array.strides;
    auto rank = (int) shape.count;

    if (strides[(NSUInteger) rank - 1].integerValue != 1)
        return false;

    for (auto axis = rank - 3; axis >= 0; --axis)
        if (strides[(NSUInteger) axis].integerValue
            != strides[(NSUInteger) axis + 1].integerValue
                   * shape[(NSUInteger) axis + 1].integerValue)
            return false;

    auto rowElements = rank >= 2 ? strides[(NSUInteger) rank - 2].integerValue
                                 : shape.lastObject.integerValue;
    stride = (size_t) rowElements * (size_t) elementSize;
    return true;
}

std::shared_ptr<MultiArray::Native> makePlainNative(const Shape& shape, DType type)
{
    auto dataType = MLMultiArrayDataTypeFloat32;

    if (!toMLDataType(type, dataType))
        return {};

    auto pool = ObjC::AutoReleasePool {};

    NSError* error = nil;
    auto array = ObjC::Ptr<MLMultiArray> {[[MLMultiArray alloc]
        initWithShape:multiArrayShapeToNSArray(shape)
             dataType:dataType
                error:&error]};

    if (!array)
        return {};

    auto native = std::make_shared<MultiArray::Native>();
    native->shape = shape;
    native->type = type;
    native->array = array;
    native->bytes = static_cast<std::byte*>(array.get().dataPointer);

    if (native->bytes == nullptr
        || !readRowStride(
            array.get(), multiArrayElementSizeOf(type), native->stride))
        return {};

    std::memset(native->bytes, 0, native->stride * (size_t) (shape.count() / shape.dims.back()));
    return native;
}

std::shared_ptr<MultiArray::Native> makeSurfaceNative(const Shape& shape)
{
    if (@available(macOS 12.0, iOS 16.0, *))
    {
        auto pool = ObjC::AutoReleasePool {};
        auto columns = shape.dims.back();
        auto rows = (int) (shape.count() / columns);

        NSDictionary* attributes = @{
            (id) kCVPixelBufferIOSurfacePropertiesKey: @{},
            (id) kCVPixelBufferMetalCompatibilityKey: @YES,
        };

        CVPixelBufferRef buffer = nullptr;
        auto status = CVPixelBufferCreate(kCFAllocatorDefault,
                                          (size_t) columns,
                                          (size_t) rows,
                                          kCVPixelFormatType_OneComponent16Half,
                                          (__bridge CFDictionaryRef) attributes,
                                          &buffer);

        if (status != kCVReturnSuccess || buffer == nullptr)
            return {};

        auto native = std::make_shared<MultiArray::Native>();
        native->shape = shape;
        native->type = DType::float16;
        native->surface.reset(buffer);
        native->stride = CVPixelBufferGetBytesPerRow(buffer);
        auto dimensions = multiArrayShapeToNSArray(shape);
        native->array = [[MLMultiArray alloc] initWithPixelBuffer:buffer
                                                            shape:dimensions];

        if (!native->array)
            return {};

        auto access = CpuAccess {*native, false};
        std::memset(access.data(), 0, native->stride * (size_t) rows);
        return native;
    }

    return {};
}

std::shared_ptr<MultiArray::Native> wrapSurface(MLMultiArray* output,
                                                CVPixelBufferRef buffer)
{
    auto native = std::make_shared<MultiArray::Native>();
    native->shape = toShape(output.shape);
    native->type = DType::float16;
    native->array.reset(output);
    native->surface.reset(CVPixelBufferRetain(buffer));
    native->stride = CVPixelBufferGetBytesPerRow(buffer);
    return native;
}

class RowOffsets
{
public:
    explicit RowOffsets(MLMultiArray* array)
    {
        for (NSNumber* extent in array.shape)
            extents.add(extent.intValue);

        for (NSNumber* stride in array.strides)
            strides.add(stride.intValue);
    }

    size_t of(int row) const
    {
        auto offset = size_t {0};

        for (auto axis = extents.size() - 2; axis >= 0; --axis)
        {
            offset += (size_t) (row % extents[axis]) * (size_t) strides[axis];
            row /= extents[axis];
        }

        return offset;
    }

    int columnStride() const { return strides.back(); }

private:
    Vector<int> extents;
    Vector<int> strides;
};

void copyStridedInto(MultiArray& copy, MLMultiArray* output)
{
    if (@available(macOS 12.3, iOS 15.4, *))
    {
        auto access = CpuAccess {*copy.native(), false};
        auto destination = access.data();
        auto destinationStride = copy.rowStride();
        auto type = copy.type();
        auto rows = copy.rows();
        auto columns = copy.columns();
        auto elementSize = (size_t) multiArrayElementSizeOf(type);
        auto offsets = RowOffsets {output};
        auto columnStride = (size_t) offsets.columnStride();

        auto readRows = ^(const void* bytes, NSInteger)
        {
            auto source = static_cast<const std::byte*>(bytes);

            for (auto row = 0; row < rows; ++row)
            {
                auto from = source + offsets.of(row) * elementSize;
                auto to = destination + (size_t) row * destinationStride;

                if (columnStride == 1)
                {
                    std::memcpy(to, from, (size_t) columns * elementSize);
                    continue;
                }

                for (auto column = 0; column < columns; ++column)
                    std::memcpy(to + (size_t) column * elementSize,
                                from + (size_t) column * columnStride * elementSize,
                                elementSize);
            }
        };

        [output getBytesWithHandler:readRows];
    }
}
} // namespace

MLMultiArray* nativeArray(const MultiArray& array)
{
    auto native = array.native();
    return native ? native->array.get() : nil;
}

MultiArray adoptOutput(MLMultiArray* output)
{
    auto type = DType::float32;

    if (output == nil || !toDType(output.dataType, type))
        return {};

    if (@available(macOS 12.0, iOS 16.0, *))
        if (auto buffer = output.pixelBuffer)
            return MultiArray {wrapSurface(output, buffer)};

    auto copy = MultiArray::create(toShape(output.shape), type);

    if (copy.isValid())
        copyStridedInto(copy, output);

    return copy;
}

MultiArray::MultiArray() = default;

MultiArray::MultiArray(const std::shared_ptr<Native>& nativeToUse)
    : impl(nativeToUse)
{
}

std::shared_ptr<MultiArray::Native> MultiArray::native() const
{
    return impl;
}

MultiArray MultiArray::create(const Shape& shape, DType type)
{
    if (!isAllocatable(shape))
        return {};

    if (type == DType::float16)
        if (auto surface = makeSurfaceNative(shape))
            return MultiArray {surface};

    return MultiArray {makePlainNative(shape, type)};
}

bool MultiArray::isValid() const
{
    return impl != nullptr;
}

bool MultiArray::isSurfaceBacked() const
{
    return impl != nullptr && impl->surface;
}

const Shape& MultiArray::shape() const
{
    static const auto empty = Shape {};
    return impl ? impl->shape : empty;
}

DType MultiArray::type() const
{
    return impl ? impl->type : DType::float32;
}

int MultiArray::elementCount() const
{
    return impl ? (int) impl->shape.count() : 0;
}

int MultiArray::columns() const
{
    return impl ? impl->shape.dims.back() : 0;
}

int MultiArray::rows() const
{
    auto count = columns();
    return count == 0 ? 0 : elementCount() / count;
}

int MultiArray::elementSize() const
{
    return multiArrayElementSizeOf(type());
}

size_t MultiArray::rowStride() const
{
    return impl ? impl->stride : 0;
}

size_t MultiArray::byteCount() const
{
    return rowStride() * (size_t) rows();
}

Vector<float> MultiArray::toFloats() const
{
    auto values = Vector<float> {};

    if (!isValid())
        return values;

    auto access = CpuAccess {*impl, true};
    values.resize(elementCount(), 0.0f);
    multiArrayConvertRows(access.data(),
                type(),
                rowStride(),
                values.data(),
                DType::float32,
                (size_t) columns() * sizeof(float),
                rows(),
                columns());
    return values;
}

void MultiArray::fromFloats(Span<const float> values)
{
    if (!isValid())
        return;

    auto access = CpuAccess {*impl, false};
    auto packedStride = (size_t) columns() * sizeof(float);
    auto wholeRows = std::min((int) values.size() / columns(), rows());

    multiArrayConvertRows(values.data(),
                DType::float32,
                packedStride,
                access.data(),
                type(),
                rowStride(),
                wholeRows,
                columns());

    auto remainder = std::min((int) values.size(), elementCount()) - wholeRows * columns();

    if (remainder > 0)
        multiArrayConvertRows(values.data() + wholeRows * columns(),
                    DType::float32,
                    packedStride,
                    access.data() + (size_t) wholeRows * rowStride(),
                    type(),
                    rowStride(),
                    1,
                    remainder);
}

namespace
{
size_t bufferRowBytes(const MultiArray& array, DType bufferType)
{
    return (size_t) array.columns() * (size_t) multiArrayElementSizeOf(bufferType);
}

bool fitsBuffer(const MultiArray& array,
                const GPU::Buffer& buffer,
                int offset,
                size_t bufferRowStride,
                DType bufferType)
{
    auto rowBytes = bufferRowBytes(array, bufferType);

    if (!array.isValid() || !buffer.isValid() || offset < 0
        || bufferRowStride < rowBytes)
        return false;

    auto lastRow = bufferRowStride * (size_t) (array.rows() - 1);
    return (size_t) offset + lastRow + rowBytes <= (size_t) buffer.size();
}
} // namespace

void MultiArray::copyTo(GPU::Buffer& buffer, DType bufferType) const
{
    copyTo(buffer, 0, bufferRowBytes(*this, bufferType), bufferType);
}

void MultiArray::copyFrom(const GPU::Buffer& buffer, DType bufferType)
{
    copyFrom(buffer, 0, bufferRowBytes(*this, bufferType), bufferType);
}

void MultiArray::copyTo(GPU::Buffer& buffer,
                        int offset,
                        size_t bufferRowStride,
                        DType bufferType) const
{
    if (!fitsBuffer(*this, buffer, offset, bufferRowStride, bufferType))
        return;

    auto access = CpuAccess {*impl, true};
    auto rowBytes = bufferRowBytes(*this, bufferType);
    auto isPacked = bufferRowStride == rowBytes;
    auto source = access.data();
    auto sourceStride = rowStride();
    auto staging = Vector<std::byte> {};

    if (bufferType != type() || (isPacked && sourceStride != rowBytes))
    {
        staging.resize((int) (rowBytes * (size_t) rows()), std::byte {0});
        multiArrayConvertRows(access.data(),
                    type(),
                    rowStride(),
                    staging.data(),
                    bufferType,
                    rowBytes,
                    rows(),
                    columns());
        source = staging.data();
        sourceStride = rowBytes;
    }

    if (isPacked)
    {
        buffer.update(source, (int) (rowBytes * (size_t) rows()), offset);
        return;
    }

    for (auto row = 0; row < rows(); ++row)
        buffer.update(source + (size_t) row * sourceStride,
                      (int) rowBytes,
                      offset + (int) ((size_t) row * bufferRowStride));
}

void MultiArray::copyFrom(const GPU::Buffer& buffer,
                          int offset,
                          size_t bufferRowStride,
                          DType bufferType)
{
    if (!fitsBuffer(*this, buffer, offset, bufferRowStride, bufferType))
        return;

    auto access = CpuAccess {*impl, false};
    auto rowBytes = bufferRowBytes(*this, bufferType);
    auto span = bufferRowStride * (size_t) (rows() - 1) + rowBytes;

    if (bufferType == type() && rowStride() == rowBytes
        && bufferRowStride == rowBytes)
    {
        buffer.read(access.data(), (int) span, offset);
        return;
    }

    auto staging = Vector<std::byte> {};
    staging.resize((int) span, std::byte {0});
    buffer.read(staging.data(), (int) span, offset);
    multiArrayConvertRows(staging.data(),
                bufferType,
                bufferRowStride,
                access.data(),
                type(),
                rowStride(),
                rows(),
                columns());
}

void MultiArray::copyFrom(const MultiArray& other)
{
    if (!isValid() || !other.isValid() || other.impl == impl)
        return;

    if (other.columns() == columns() && other.rows() == rows())
    {
        auto source = CpuAccess {*other.impl, true};
        auto destination = CpuAccess {*impl, false};
        multiArrayConvertRows(source.data(),
                    other.type(),
                    other.rowStride(),
                    destination.data(),
                    type(),
                    rowStride(),
                    rows(),
                    columns());
        return;
    }

    auto values = other.toFloats();
    fromFloats(values);
}

void MultiArray::copyRows(const MultiArray& source,
                          int sourceRow,
                          int destinationRow,
                          int rowCount)
{
    auto inside = [](int first, int count, int rows)
    { return first >= 0 && count > 0 && first + count <= rows; };

    if (!isValid() || !source.isValid() || source.impl == impl
        || source.columns() != columns()
        || !inside(sourceRow, rowCount, source.rows())
        || !inside(destinationRow, rowCount, rows()))
        return;

    auto from = CpuAccess {*source.impl, true};
    auto to = CpuAccess {*impl, false};

    multiArrayConvertRows(from.data() + (size_t) sourceRow * source.rowStride(),
                source.type(),
                source.rowStride(),
                to.data() + (size_t) destinationRow * rowStride(),
                type(),
                rowStride(),
                rowCount,
                columns());
}
} // namespace eacp::ML
