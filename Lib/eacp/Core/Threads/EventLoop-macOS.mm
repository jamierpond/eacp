#include "EventLoop.h"
#include "../ObjC/ObjC.h"
#import <Foundation/Foundation.h>
#import <Cocoa/Cocoa.h>

#include <cassert>
#include <chrono>

// terminate: (Cmd+Q, Dock quit, quit Apple Events) calls exit() without
// unwinding run<T>(); cancel it and stop the loop so the normal teardown runs.
@interface AppTerminationBridge : NSObject <NSApplicationDelegate>
@end

@implementation AppTerminationBridge
- (NSApplicationTerminateReply)applicationShouldTerminate:(NSApplication*)sender
{
    eacp::Threads::getEventLoop().quit();
    return NSTerminateCancel;
}
@end

namespace eacp::Threads
{
namespace
{
auto s_inRootRunLoop = false;
auto s_nestedDepth = 0;
auto s_quitRequested = false;

NSApplicationActivationPolicy activationPolicyFromBundle()
{
    auto* info = [[NSBundle mainBundle] infoDictionary];

    if ([info[@"LSBackgroundOnly"] boolValue])
        return NSApplicationActivationPolicyProhibited;

    if ([info[@"LSUIElement"] boolValue])
        return NSApplicationActivationPolicyAccessory;

    return NSApplicationActivationPolicyRegular;
}

NSApplication* getApp()
{
    static NSApplication* app = [] {
        auto* application = [NSApplication sharedApplication];
        [application setActivationPolicy:activationPolicyFromBundle()];

        static auto delegate =
            ObjC::Ptr<AppTerminationBridge>([[AppTerminationBridge alloc] init]);
        [application setDelegate:delegate.get()];

        return application;
    }();
    return app;
}

// Single wrapper around [NSApp run]. Apple says NSApplication's run
// is not safe to call recursively, so the guard here keeps the
// invariant in one place: callers that need a bounded inner loop
// use EventLoop::runFor (polled) instead.
void enterRootRunLoop()
{
    assert(! s_inRootRunLoop
           && "EventLoop::run must not be called recursively. "
              "Use EventLoop::runFor for nested loops.");

    s_inRootRunLoop = true;
    s_quitRequested = false;
    [getApp() run];
    s_inRootRunLoop = false;
}

NSEvent* makeWakeEvent()
{
    return [NSEvent otherEventWithType:NSEventTypeApplicationDefined
                              location:NSMakePoint(0, 0)
                         modifierFlags:0
                             timestamp:0
                          windowNumber:0
                               context:nil
                               subtype:0
                                 data1:0
                                 data2:0];
}
} // namespace

void EventLoop::run()
{
    enterRootRunLoop();
}

bool EventLoop::runFor(std::chrono::milliseconds timeout)
{
    s_nestedDepth++;
    s_quitRequested = false;

    auto deadline = std::chrono::steady_clock::now() + timeout;

    while (! s_quitRequested)
    {
        auto now = std::chrono::steady_clock::now();
        if (now >= deadline)
        {
            s_nestedDepth--;
            return false;
        }

        auto remainingSecs =
            std::chrono::duration<double>(deadline - now).count();
        auto* date = [NSDate dateWithTimeIntervalSinceNow:remainingSecs];

        auto* event = [getApp() nextEventMatchingMask:NSEventMaskAny
                                            untilDate:date
                                               inMode:NSDefaultRunLoopMode
                                              dequeue:YES];
        if (event)
            [getApp() sendEvent:event];
    }

    s_quitRequested = false;
    s_nestedDepth--;
    return true;
}

// [NSApp stop:] applies to the active [NSApp run]. If a nested runFor is on
// top, its polling loop reads s_quitRequested directly — we only need to
// wake it via the dummy event.
void EventLoop::quit()
{
    s_quitRequested = true;

    if (s_nestedDepth == 0 && s_inRootRunLoop)
        [getApp() stop:nil];

    [getApp() postEvent:makeWakeEvent() atStart:YES];
}

void scheduleStartup(const Callback& func)
{
    callAsync(func);
}
} // namespace eacp::Threads
