
#import <Cocoa/Cocoa.h>
#include "View.h"
#include "../Graphics/GraphicsContextImpl.h"
#include "../Image/Image.h"
#include "../Window/MouseLock-macOS.h"

#include <eacp/Core/Threads/Async.h>
#include "../Graphics/Keyboard-MacOS.h"

#include <eacp/Core/ObjC/RuntimeClass.h>

namespace eacp::Graphics
{
namespace
{
Class getNativeViewClass();

View* getView(id self)
{
    return (View*) ObjC::getIvar<void*>(self, "cppView");
}

NSView* getRootView(id self)
{
    auto* root = (NSView*) self;
    NSView* current = [root superview];

    while (current != nil)
    {
        if ([current isKindOfClass:getNativeViewClass()])
            root = current;

        current = current.superview;
    }

    return root;
}

MouseButton mouseButtonFromEvent(NSEvent* event)
{
    switch (event.buttonNumber)
    {
        case 0:
            return MouseButton::Left;
        case 1:
            return MouseButton::Right;
        case 2:
            return MouseButton::Middle;
        default:
            return MouseButton::Other;
    }
}

void dispatchMouseEvent(id self, NSEvent* event, MouseEventType type)
{
    auto* root = getRootView(self);
    auto windowPos = [event locationInWindow];
    auto localPos = [root convertPoint:windowPos fromView:nil];

    auto e = MouseEvent();

    e.pos = {(float) localPos.x, (float) localPos.y};
    e.type = type;
    e.button = mouseButtonFromEvent(event);
    e.modifiers = modifierKeysFromEvent(event);

    e.timestamp = event.timestamp;

    // Both figures, always: `delta` is the pointer's movement, shaped by the
    // system's acceleration curve, and `rawDelta` is the device's own. A widget
    // dragged by the pointer wants the first; a camera wants the second. See
    // MouseEvent.
    e.delta = {(float) event.deltaX, (float) event.deltaY};
    e.rawDelta = e.delta;

    if (CGEventRef cgEvent = [event CGEvent])
        e.rawDelta = {(float) CGEventGetIntegerValueField(
                          cgEvent, kCGEventUnacceleratedPointerMovementX),
                      (float) CGEventGetIntegerValueField(
                          cgEvent, kCGEventUnacceleratedPointerMovementY)};

    // A warp is not motion, however the system reports it.
    if (detail::cursorWasWarped)
    {
        e.delta = {};
        e.rawDelta = {};
        detail::cursorWasWarped = false;
    }

    if (type == MouseEventType::Down || type == MouseEventType::Up)
    {
        e.pressure = event.pressure;
        e.clickCount = (int) event.clickCount;
    }

    auto& mouseDownPosition = ObjC::getIvar<NSPoint>(root, "mouseDownPosition");

    if (type == MouseEventType::Down)
        mouseDownPosition = localPos;

    e.downPos = {(float) mouseDownPosition.x, (float) mouseDownPosition.y};

    if (auto* view = getView(root))
        view->dispatchMouseEvent(e);
}

ScrollPhase scrollPhaseFromEvent(NSEvent* event)
{
    // Momentum is reported on its own phase mask; it is checked first because an
    // event coasting after lift-off carries momentumPhase set and phase empty.
    switch (event.momentumPhase)
    {
        case NSEventPhaseBegan:
        case NSEventPhaseChanged:
            return ScrollPhase::Momentum;
        case NSEventPhaseEnded:
        case NSEventPhaseCancelled:
            return ScrollPhase::MomentumEnded;
        default:
            break;
    }

    switch (event.phase)
    {
        case NSEventPhaseBegan:
        case NSEventPhaseMayBegin:
            return ScrollPhase::Began;
        case NSEventPhaseChanged:
            return ScrollPhase::Changed;
        case NSEventPhaseEnded:
        case NSEventPhaseCancelled:
            return ScrollPhase::Ended;
        default:
            break;
    }

    // A notched wheel reports no phase at all.
    return ScrollPhase::None;
}

void scrollWheel(id self, SEL, NSEvent* event)
{
    auto* root = getRootView(self);
    auto windowPos = [event locationInWindow];
    auto localPos = [root convertPoint:windowPos fromView:nil];

    auto e = MouseEvent();

    e.pos = {(float) localPos.x, (float) localPos.y};
    e.type = MouseEventType::Wheel;
    e.button = MouseButton::Other;
    e.modifiers = modifierKeysFromEvent(event);
    e.timestamp = event.timestamp;
    e.preciseScrolling = event.hasPreciseScrollingDeltas == YES;
    e.scrollPhase = scrollPhaseFromEvent(event);

    // scrollingDelta*, not delta*: the scrolling pair carries the precise
    // per-point figure on a trackpad, where deltaY has already been quantised
    // back into whole lines and the smoothness is gone.
    e.delta = {(float) event.scrollingDeltaX, (float) event.scrollingDeltaY};
    e.rawDelta = e.delta;

    e.downPos = e.pos;

    if (auto* view = getView(root))
        view->dispatchMouseEvent(e);
}

void drawRect(id, SEL, NSRect)
{
}

void drawLayerInContext(id self, SEL, CALayer*, CGContextRef ctx)
{
    if (auto* view = getView(self))
    {
        auto nativeContext = MacOSContext(ctx);
        view->paint(nativeContext);
    }
}

void layout(id self, SEL)
{
    ObjC::sendSuper<void>(self, [NSView class], @selector(layout));

    if (auto* view = getView(self))
        view->resized();
}

BOOL isFlipped(id, SEL)
{
    return YES;
}

BOOL isOpaque(id, SEL)
{
    return NO;
}

BOOL acceptsFirstResponder(id, SEL)
{
    return YES;
}

void viewDidChangeBackingProperties(id self, SEL)
{
    ObjC::sendSuper<void>(
        self, [NSView class], @selector(viewDidChangeBackingProperties));

    auto* view = (NSView*) self;
    view.layer.contentsScale = view.window.backingScaleFactor;

    // The base layer follows the new scale by itself, but anything the C++ side
    // sized in device pixels (a CAMetalLayer's drawableSize, a glyph atlas) does
    // not, so tell the view.
    if (auto* eacpView = getView(self))
        eacpView->backingScaleChanged();
}

void setFrame(id self, SEL, NSRect newFrame)
{
    auto* view = (NSView*) self;
    auto oldFrame = [view frame];

    ObjC::sendSuper<void>(self, [NSView class], @selector(setFrame:), newFrame);

    if (!NSEqualSizes(oldFrame.size, newFrame.size))
    {
        [view setNeedsDisplay:YES];

        if (view.wantsLayer && view.layer)
            [view.layer setNeedsDisplay];
    }
}

void mouseDown(id self, SEL, NSEvent* event)
{
    dispatchMouseEvent(self, event, MouseEventType::Down);
}

void mouseUp(id self, SEL, NSEvent* event)
{
    dispatchMouseEvent(self, event, MouseEventType::Up);
}

void mouseDragged(id self, SEL, NSEvent* event)
{
    dispatchMouseEvent(self, event, MouseEventType::Dragged);
}

void mouseMoved(id self, SEL, NSEvent* event)
{
    dispatchMouseEvent(self, event, MouseEventType::Moved);
}

void mouseEntered(id self, SEL, NSEvent* event)
{
    dispatchMouseEvent(self, event, MouseEventType::Entered);
}

void mouseExited(id self, SEL, NSEvent* event)
{
    dispatchMouseEvent(self, event, MouseEventType::Exited);
}

void keyDown(id self, SEL, NSEvent* event)
{
    if (auto* view = getView(self))
        view->keyDown(keyEventFrom(event, KeyEventType::Down));
}

void keyUp(id self, SEL, NSEvent* event)
{
    if (auto* view = getView(self))
        view->keyUp(keyEventFrom(event, KeyEventType::Up));
}

void flagsChanged(id self, SEL, NSEvent* event)
{
    // A modifier changed with no character to show for it (press or release).
    // AppKit delivers this to the first responder, same as keyDown:, so the
    // focused view hears its chords being held and let go.
    if (auto* view = getView(self))
        view->modifiersChanged(modifierKeysFromEvent(event));
}

NSCursor* toNSCursor(MouseCursor cursor)
{
    switch (cursor)
    {
        case MouseCursor::IBeam:
            return [NSCursor IBeamCursor];
        case MouseCursor::PointingHand:
            return [NSCursor pointingHandCursor];
        case MouseCursor::ResizeLeftRight:
            return [NSCursor resizeLeftRightCursor];
        case MouseCursor::ResizeUpDown:
            return [NSCursor resizeUpDownCursor];
        case MouseCursor::Crosshair:
            return [NSCursor crosshairCursor];
        case MouseCursor::Default:
            break;
    }

    return [NSCursor arrowCursor];
}

// AppKit asks this whenever the pointer enters the tracking area, and after
// anything else has had a go at setting the cursor. Without it, a shape set
// from a mouseMoved handler survives only until the pointer crosses a boundary
// and AppKit resets it — which reads as the cursor flickering back at random
// rather than as a missing method.
void cursorUpdate(id self, SEL, id)
{
    if (auto* view = getView(self))
        [toNSCursor(view->getMouseCursor()) set];
}

void updateTrackingAreas(id self, SEL)
{
    ObjC::sendSuper<void>(self, [NSView class], @selector(updateTrackingAreas));

    auto* view = (NSView*) self;

    for (NSTrackingArea* area in view.trackingAreas)
        [view removeTrackingArea:area];

    NSTrackingAreaOptions options =
        NSTrackingMouseEnteredAndExited | NSTrackingMouseMoved
        | NSTrackingActiveInKeyWindow | NSTrackingInVisibleRect
        | NSTrackingCursorUpdate;

    NSTrackingArea* trackingArea =
        [[NSTrackingArea alloc] initWithRect:view.bounds
                                     options:options
                                       owner:view
                                    userInfo:nil];
    [view addTrackingArea:trackingArea];
    [trackingArea release];
}

Class getNativeViewClass()
{
    static auto instance = []
    {
        auto builder = new ObjC::RuntimeClass<NSView>("EacpNativeView");

        builder->addIvar<void*>("cppView");
        builder->addIvar<NSPoint>("mouseDownPosition");

        builder->addProtocol(@protocol(CALayerDelegate));

        builder->addMethod(@selector(drawRect:), drawRect);
        builder->addMethod(@selector(drawLayer:inContext:), drawLayerInContext);
        builder->addMethod(@selector(layout), layout);
        builder->addMethod(@selector(isFlipped), isFlipped);
        builder->addMethod(@selector(isOpaque), isOpaque);
        builder->addMethod(@selector(acceptsFirstResponder),
                           acceptsFirstResponder);
        builder->addMethod(@selector(viewDidChangeBackingProperties),
                           viewDidChangeBackingProperties);
        builder->addMethod(@selector(setFrame:), setFrame);

        builder->addMethod(@selector(mouseDown:), mouseDown);
        builder->addMethod(@selector(mouseUp:), mouseUp);
        builder->addMethod(@selector(mouseDragged:), mouseDragged);
        builder->addMethod(@selector(mouseMoved:), mouseMoved);
        builder->addMethod(@selector(mouseEntered:), mouseEntered);
        builder->addMethod(@selector(mouseExited:), mouseExited);
        builder->addMethod(@selector(scrollWheel:), scrollWheel);
        builder->addMethod(@selector(rightMouseDown:), mouseDown);
        builder->addMethod(@selector(rightMouseUp:), mouseUp);
        builder->addMethod(@selector(rightMouseDragged:), mouseDragged);
        builder->addMethod(@selector(otherMouseDown:), mouseDown);
        builder->addMethod(@selector(otherMouseUp:), mouseUp);
        builder->addMethod(@selector(otherMouseDragged:), mouseDragged);

        builder->addMethod(@selector(keyDown:), keyDown);
        builder->addMethod(@selector(keyUp:), keyUp);
        builder->addMethod(@selector(flagsChanged:), flagsChanged);
        builder->addMethod(@selector(cursorUpdate:), cursorUpdate);
        builder->addMethod(@selector(updateTrackingAreas),
                           updateTrackingAreas);

        builder->registerClass();
        return builder;
    }();

    return instance->get();
}

NSView* createNativeView(View* view)
{
    auto rect = NSMakeRect(0.f, 0.f, 100.f, 100.f);
    NSView* newView = [[getNativeViewClass() alloc] initWithFrame:rect];

    newView.wantsLayer = YES;
    newView.layerContentsRedrawPolicy = NSViewLayerContentsRedrawOnSetNeedsDisplay;
    newView.layer.contentsScale = [NSScreen mainScreen].backingScaleFactor;
    newView.layer.delegate = (id<CALayerDelegate>) newView;

    ObjC::getIvar<void*>(newView, "cppView") = view;
    return newView;
}
} // namespace

struct View::Native
{
    Native(View& view) { nativeView = createNativeView(&view); }

    // AppKit and CoreAnimation can outlive our ObjC::Ptr reference — a pending
    // CA transaction still draws the backing layer after the C++ View died
    // (e.g. a window torn down right after a repaint). Break both back-links so
    // a surviving NativeView is inert instead of dereferencing a freed View.
    ~Native()
    {
        auto* view = nativeView.get();
        ObjC::getIvar<void*>(view, "cppView") = nullptr;
        view.layer.delegate = nil;
        [view removeFromSuperview];
    }

    void repaint() { [nativeView.get() setNeedsDisplay:YES]; }

    void setOpacity(float opacity) { nativeView.get().alphaValue = opacity; }

    Rect getBounds() const { return toRect([nativeView.get() frame]); }
    void setBounds(const Rect& bounds)
    {
        auto frame = toCGRect(bounds);
        [nativeView.get() setFrame:frame];
    }

    float backingScale() const
    {
        auto* view = nativeView.get();
        auto scale = view.window != nil ? view.window.backingScaleFactor
                                        : [NSScreen mainScreen].backingScaleFactor;

        // No window and no screen (headless) reports 0; fall back to 1:1 so the
        // default-scale snapshot still produces pixels.
        return scale > 0.0 ? (float) scale : 1.0f;
    }

    void addSubview(View& view)
    {
        auto* childNativeView = (NSView*) view.getHandle();
        [nativeView.get() addSubview:childNativeView];
    }

    void removeSubview(View& view)
    {
        auto* childNativeView = (NSView*) view.getHandle();
        [childNativeView removeFromSuperview];
    }

    CALayer* getLayer() { return nativeView.get().layer; }

    Point getMousePosition() const
    {
        auto view = nativeView.get();
        auto windowPoint = [view.window mouseLocationOutsideOfEventStream];
        auto localPoint = [view convertPoint:windowPoint fromView:nil];

        return {(float) localPoint.x, (float) localPoint.y};
    }

    void focus()
    {
        auto view = nativeView.get();
        [view.window makeFirstResponder:view];
    }

    bool hasFocus() const
    {
        auto view = nativeView.get();
        return view.window.firstResponder == view;
    }

    ObjC::Ptr<NSView> nativeView;
};

View::View()
    : impl(*this)
{
}

View::~View()
{
    for (auto* layer: layers)
        layer->detachFromLayer();

    removeFromParent();
}

void* View::getHandle()
{
    return impl->nativeView.get();
}

void View::repaint()
{
    impl->repaint();
}

void View::setOpacity(float opacityToUse)
{
    opacity = opacityToUse;
    impl->setOpacity(opacityToUse);
}

Rect View::getBounds() const
{
    return impl->getBounds();
}

Image View::renderToImage(float scale)
{
    auto resolvedScale = scale > 0.0f ? scale : impl->backingScale();
    return renderLayerToImage(*this, getLocalBounds(), resolvedScale);
}

Threads::Async<Image> View::renderToImageAsync(float scale)
{
    auto resolvedScale = scale > 0.0f ? scale : impl->backingScale();
    return renderViewToImageAsync(*this, getLocalBounds(), resolvedScale);
}

Point View::getMousePosition() const
{
    return impl->getMousePosition();
}

void View::setMouseCursor(MouseCursor cursor)
{
    if (currentCursor == cursor)
        return;

    currentCursor = cursor;

    // Applied here as well as from cursorUpdate:, because the call that changes
    // it almost always comes *from* a mouseMoved handler — the pointer is
    // already inside, and inside is the only time the shape is visible. Waiting
    // for the next cursorUpdate: would leave the old shape under a pointer that
    // has already crossed onto the splitter.
    auto* native = (NSView*) impl->nativeView.get();

    if (native != nil && native.window != nil)
        [toNSCursor(cursor) set];
}

void View::focus()
{
    impl->focus();
}

bool View::hasFocus() const
{
    return impl->hasFocus();
}

void View::setBounds(const Rect& bounds)
{
    impl->setBounds(bounds);
}

void View::viewAdded(View& view)
{
    impl->addSubview(view);
}

void View::viewRemoved(View& view)
{
    impl->removeSubview(view);
}

void* View::getNativeLayer()
{
    return impl->getLayer();
}
} // namespace eacp::Graphics
